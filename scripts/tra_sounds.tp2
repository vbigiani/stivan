BACKUP ~scripts/backup~
AUTHOR ~tb~

BEGIN ~Populate TRA sound references~

NO_LOG_RECORD

<<<<<<<< names-to-soundsets
Description prefix
STIVAN TB#ST
MITSU TB#SM
CUCHUL TB#SC
ESTELNECRI TB#SE

>>>>>>>>

<<<<<<<< empty
>>>>>>>>
COPY - ~names-to-soundsets~ ~names-to-soundsets~
	READ_2DA_ENTRIES_NOW names2da 2
	FOR (i = 1; i < names2da; ++i) BEGIN
		READ_2DA_ENTRY_FORMER names2da i 0 name
		READ_2DA_ENTRY_FORMER names2da i 1 prefix
		TEXT_SPRINT $names(EVALUATE_BUFFER ~%name%~) ~%prefix%~
		SET $sizes(EVALUATE_BUFFER ~%name%~) = 8 - STRING_LENGTH ~%prefix%~
		SET $last(EVALUATE_BUFFER ~%name%~) = 0
		INNER_ACTION BEGIN
			COPY - ~empty~ ~%name%.tmp~
		END
	END
BUT_ONLY

COPY	~stivan/tra/english/setup.tra~ ~stivan/tra/english/setup.tra~
		~stivan/tra/english~ ~stivan/tra/english~
	PATCH_SILENT
	REPLACE_EVALUATE "\(~[^~]*~\) \[\([^]]*\)\]%LNL%\(@[0-9]*\)" BEGIN
		TEXT_SPRINT result ~%MATCH1% [%MATCH2%]%LNL%%MATCH3%~
		TEXT_SPRINT EVALUATE_BUFFER ~%SOURCE_RES% %MATCH3%~ ~%MATCH2%~
		PATCH_IF VARIABLE_IS_SET $names(EVALUATE_BUFFER ~%MATCH2%~) THEN BEGIN
			i = $last(EVALUATE_BUFFER ~%MATCH2%~)
			i += 1
			SET $last(EVALUATE_BUFFER ~%MATCH2%~) = i
			TEXT_SPRINT str ~%i%~
			SET length = $sizes(EVALUATE_BUFFER ~%MATCH2%~)
			TEXT_SPRINT prefix $names(EVALUATE_BUFFER ~%MATCH2%~)
			WHILE STRING_LENGTH ~%str%~ < length BEGIN
				TEXT_SPRINT str ~0%str%~
			END
			TEXT_SPRINT EVALUATE_BUFFER ~%SOURCE_RES% %MATCH3%~ ~%MATCH2%%str%~
			TEXT_SPRINT result ~%MATCH1% [%prefix%%str%]%LNL%%MATCH3%~
			
			INNER_ACTION BEGIN
				APPEND_OUTER - ~%MATCH2%.tmp~ ~%MATCH1% [%prefix%%str%]~
			END
		END
	END ~%result%~
BUT_ONLY

VERBOSE

COPY ~stivan/tra/italian~ ~stivan/tra/italian~
	REPLACE_EVALUATE "~%LNL%\(@[0-9]*\)" BEGIN
		TEXT_SPRINT result ~%LNL%%MATCH1%~
		PATCH_IF VARIABLE_IS_SET EVALUATE_BUFFER ~%SOURCE_RES% %MATCH1%~ THEN BEGIN
			TEXT_SPRINT str ~%SOURCE_RES% %MATCH1%~
			TEXT_SPRINT str EVALUATE_BUFFER ~%%str%%~
			TEXT_SPRINT result ~ [%str%]%LNL%%MATCH1%~
		END
	END "~%result%"
BUT_ONLY

COPY - ~names-to-soundsets~ ~names-to-soundsets~
	FOR (i = 1; i < names2da; ++i) BEGIN
		READ_2DA_ENTRY_FORMER names2da i 0 name
		
		INNER_ACTION BEGIN
			COPY ~to_be_merged/sounds_to_record/%name%.txt~ ~to_be_merged/sounds_to_record/%name%.txt~
				APPEND_FILE ~%name%.tmp~
				REPLACE_TEXTUALLY ~%LNL%%LNL%*~ ~%LNL%~
		END
	END
BUT_ONLY
