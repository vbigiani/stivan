BACKUP ~scripts/backup~
AUTHOR ~tb~

BEGIN ~Populate TRA sound references~

NO_LOG_RECORD

<<<<<<<< names-to-soundsets
Description prefix
STIVAN TB#SS
>>>>>>>>

OUTER_TEXT_SPRINT ~source~ ~stivan/tra/english~
ACTION_DEFINE_ARRAY ~early~ BEGIN ~setup.tra~ END
ACTION_DEFINE_ARRAY ~langs~ BEGIN ~stivan/tra/italian~ END
OUTER_TEXT_SPRINT ~sound_reference~ ~to_be_merged/sounds_to_record~



DEFINE_PATCH_MACRO read BEGIN
	REPLACE_EVALUATE "\(~[^~]*~\) \[\([^]]*\)\]%LNL%\(@[0-9]*\)" BEGIN
		TEXT_SPRINT result ~%MATCH1% [%MATCH2%]%LNL%%MATCH3%~
		TEXT_SPRINT EVALUATE_BUFFER ~%SOURCE_RES% %MATCH3%~ ~%MATCH2%~
		PATCH_IF VARIABLE_IS_SET $names(EVALUATE_BUFFER ~%MATCH2%~) THEN BEGIN
			i = $last(EVALUATE_BUFFER ~%MATCH2%~)
			i += 1
			SET $last(EVALUATE_BUFFER ~%MATCH2%~) = i
			TEXT_SPRINT str ~%i%~
			SET length = $sizes(EVALUATE_BUFFER ~%MATCH2%~)
			TEXT_SPRINT prefix $names(EVALUATE_BUFFER ~%MATCH2%~)
			WHILE STRING_LENGTH ~%str%~ < length BEGIN
				TEXT_SPRINT str ~0%str%~
			END
			TEXT_SPRINT EVALUATE_BUFFER ~%SOURCE_RES% %MATCH3%~ ~%MATCH2%%str%~
			TEXT_SPRINT result ~%MATCH1% [%prefix%%str%]%LNL%%MATCH3%~
			
			INNER_ACTION BEGIN
				APPEND_OUTER - ~%MATCH2%.tmp~ ~%MATCH1% [%prefix%%str%]~
			END
		END
	END ~%result%~
END

<<<<<<<< empty
>>>>>>>>
COPY - ~names-to-soundsets~ ~names-to-soundsets~
	READ_2DA_ENTRIES_NOW names2da 2
	FOR (i = 1; i < names2da; ++i) BEGIN
		READ_2DA_ENTRY_FORMER names2da i 0 name
		READ_2DA_ENTRY_FORMER names2da i 1 prefix
		TEXT_SPRINT $names(EVALUATE_BUFFER ~%name%~) ~%prefix%~
		SET $sizes(EVALUATE_BUFFER ~%name%~) = 8 - STRING_LENGTH ~%prefix%~
		SET $last(EVALUATE_BUFFER ~%name%~) = 0
		INNER_ACTION BEGIN
			COPY - ~empty~ ~%name%.tmp~
			
			ACTION_IF ! FILE_EXISTS ~%sound_reference%/%name%.txt~ THEN BEGIN
				COPY ~empty~ ~%sound_reference%/%name%.txt~
				APPEND_OUTER ~%sound_reference%/%name%.txt~ 0
			END ELSE BEGIN
				COPY ~%sound_reference%/%name%.txt~ ~%sound_reference%/%name%.txt~
					READ_2DA_ENTRY 0 0 0 val
					SET $last(EVALUATE_BUFFER ~%name%~) = val
				BUT_ONLY
			END
		END
	END
BUT_ONLY

SILENT

ACTION_PHP_EACH early AS from => to BEGIN
	COPY ~%source%/%to%~ ~%source%/%to%~
		LPM ~read~
	BUT_ONLY
END

COPY ~%source%~ ~%source%~
	LPM ~read~
BUT_ONLY

VERBOSE

ACTION_PHP_EACH langs AS from => to BEGIN
	COPY ~%to%~ ~%to%~
		REPLACE_EVALUATE "~%LNL%\(@[0-9]*\)" BEGIN
			TEXT_SPRINT result ~%LNL%%MATCH1%~
			PATCH_IF VARIABLE_IS_SET EVALUATE_BUFFER ~%SOURCE_RES% %MATCH1%~ THEN BEGIN
				TEXT_SPRINT str ~%SOURCE_RES% %MATCH1%~
				TEXT_SPRINT str EVALUATE_BUFFER ~%%str%%~
				TEXT_SPRINT result ~ [%str%]%LNL%%MATCH1%~
			END
		END "~%result%"
	BUT_ONLY
END

COPY - ~names-to-soundsets~ ~names-to-soundsets~
	FOR (i = 1; i < names2da; ++i) BEGIN
		READ_2DA_ENTRY_FORMER names2da i 0 name
		
		INNER_ACTION BEGIN
			COPY ~%sound_reference%/%name%.txt~ ~%sound_reference%/%name%.txt~
				SET_2DA_ENTRY 0 0 0 $last(EVALUATE_BUFFER ~%name%~)
				APPEND_FILE ~%name%.tmp~
				REPLACE_TEXTUALLY ~%LNL%%LNL%*~ ~%LNL%~
		END
	END
BUT_ONLY
