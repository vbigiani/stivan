ACTION_IF FILE_EXISTS ~override/tb#stiva.txt~ THEN BEGIN
FAIL @55
END

ACTION_IF ! (GAME_IS ~BGT TOB~) THEN BEGIN
	FAIL @56
END



// Clone misc resources


COPY_EXISTING ~uddrow35.dlg~ ~override/tb#sdr1.dlg~
		 ~uddrow35.dlg~ ~override/tb#sdr2.dlg~
		 ~uddrow35.dlg~ ~override/tb#sdr3.dlg~
	
COPY_EXISTING ~uddrow35.cre~ ~override~
	READ_ASCII 0x248 script
BUT_ONLY

COPY_EXISTING ~%script%.bcs~ ~override/tb#sdr1.bcs~
		~%script%.bcs~ ~override/tb#sdr2.bcs~
		~%script%.bcs~ ~override/tb#sdr3.bcs~

  
// Make sure that referenced scripts exist

COPY_EXISTING ~arnman04.cre~ ~override~
  READ_ASCII SCRIPT_OVERRIDE default_script
  PATCH_IF ~%default_script%~ STRING_COMPARE_CASE ~None~ &&
      ~%default_script%~ STRING_COMPARE_CASE ~~ &&
      ~%default_script%~ STRING_COMPARE_CASE ~arnman04~ THEN BEGIN
    PATCH_FAIL ~arnman04.cre has %default_script% as override script.~
  END
  WRITE_ASCII SCRIPT_OVERRIDE ~arnman04~ #8
  ADD_CRE_ITEM ~tb#spin~ #1 #0 #0 ~none~ ~INV~

  

// copy static resources

COPY ~stivan/copy~ ~override~


// Add map notes

COPY_EXISTING ~ar0500.are~ ~override~
	ADD_MAP_NOTE #4845 #2049 gray @57

		

// Create new CREs
	
COPY_EXISTING ~bhalf1.cre~ ~override~
	WRITE_ASCIIT 0x248 ~tb#stdes~

COPY_EXISTING ~seagul.cre~ ~override/tb#ssea.cre~
	WRITE_ASCII 0x280 ~tb#ssea~ #32
	
COPY_EXISTING ~uddrow35.cre~ ~override/tb#sdr1.cre~
		 ~uddrow35.cre~ ~override/tb#sdr2.cre~
		 ~uddrow35.cre~ ~override/tb#sdr3.cre~
	WRITE_EVALUATED_ASCII 0x248 ~%DEST_RES%~ #8 // override script
	WRITE_EVALUATED_ASCII 0x280 ~%DEST_RES%~ #32 // name
	WRITE_EVALUATED_ASCII 0x2cc ~%DEST_RES%~ #8 // dialogue
	
	
COPY_EXISTING ~wench1.cre~ ~override/tb#ssus.cre~
	SAY NAME1 @58
	SAY NAME2 @58
	WRITE_ASCII 0x248 ~shoutdlg~ #8 // override script
	WRITE_ASCII 0x250 ~NONE~ #8 // class script
	WRITE_ASCII 0x258 ~NONE~ #8 // race script
	WRITE_ASCII 0x260 ~NONE~ #8 // general script
	WRITE_ASCII 0x268 ~NONE~ #8 // default script
	WRITE_ASCII 0x280 ~tb#ssus~ #32 // name
	WRITE_ASCII 0x2cc ~tb#ssus~ #8 // dialogue
	
COPY_EXISTING ~nobl4.cre~ ~override/tb#scler.cre~
	SAY NAME1 @59
	SAY NAME2 @59
	WRITE_ASCII 0x248 ~NONE~ #8 // override script
	WRITE_ASCII 0x280 ~tb#scler~ #32 // name
	WRITE_ASCII 0x2cc ~tb#scler~ #8 // dialogue	

COPY_EXISTING ~noblew1.cre~ ~override/tb#stand.cre~
	SAY NAME1 @60
	SAY NAME2 @60
	WRITE_ASCII 0x248 ~tb#stand~ #8 // override script
	WRITE_ASCII 0x280 ~tb#stand~ #32 // name
	WRITE_ASCII 0x2cc ~tb#stand~ #8 // dialogue

COPY_EXISTING ~naliames.cre~ ~override/tb#smess.cre~
	SAY NAME1 @61
	SAY NAME2 @61
	WRITE_ASCII 0x248 ~tb#smess~ #8 // override script
	WRITE_ASCII 0x280 ~tb#smess~ #32 // name
	WRITE_ASCII 0x2cc ~tb#smess~ #8 // dialogue
	
COPY_EXISTING ~c6elf1.cre~ ~override/tb#self.cre~	
	SAY NAME1 @62
	SAY NAME2 @62
	WRITE_ASCII 0x248 ~tb#self~ #8 // override script
	WRITE_ASCII 0x280 ~tb#self~ #32 // name
	WRITE_ASCII 0x2cc ~tb#self~ #8 // dialogue
	
COPY_EXISTING ~sword01.cre~ ~override/tb#slil.cre~
	SAY NAME1 @63
	SAY NAME2 @63
	WRITE_ASCII 0x248 ~tb#slil~ #8 // override script
	WRITE_ASCII 0x280 ~tb#slil~ #32 // name
	WRITE_ASCII 0x2cc ~tb#slil~ #8 // dialogue
	
COPY_EXISTING ~rat.cre~ ~override/tb#srat.cre~
  WRITE_ASCII 0x248 tb#stdes #8
  
COPY_EXISTING ~vamflm01.cre~ ~override/tb#svam1.cre~
  WRITE_ASCII 0x280 ~tb#svam1~ #32
  WRITE_ASCII 0x248 ~tb#svam1~ #8
  
COPY_EXISTING ~stivan/cre/tb#sskm.cre~ ~override/tb#sskm.cre~
	SAY NAME1 @64
	SAY NAME2 @64
	WRITE_ASCII 0x2cc ~tb#sskm~ #8
	
COPY_EXISTING ~stivan/cre/tb#sskc.cre~ ~override/tb#sskc.cre~
	SAY NAME1 @65
	SAY NAME2 @65
	WRITE_ASCII 0x2cc ~tb#sskc~ #8

COPY_EXISTING ~skelwa03.cre~ ~override/tb#sskf.cre~
	SAY NAME1 @66
	SAY NAME2 @66
	WRITE_LONG  0x14  8000 // XP value
	WRITE_SHORT 0x24  150  // HP
	WRITE_SHORT 0x26  150  // ditto
	WRITE_SHORT 0x46  0    // AC
	WRITE_SHORT 0x48  0    // ditto
	WRITE_BYTE  0x234 15   // level
	WRITE_BYTE  0x238 19   // strength
	WRITE_ASCII 0x2cc ~tb#sskf~ #8
	REPLACE_TEXTUALLY CASE_INSENSITIVE ~sw2h01~ ~sw2h11~ (8)
	REPLACE_TEXTUALLY CASE_INSENSITIVE ~bow01~ ~bow02~ (8)
	
COPY ~stivan/cre/tb#stiv.cre~ ~override~
	WRITE_LONG 0x14  0 // xp value
	WRITE_LONG 0x18  161000 // current XP
	WRITE_ASCII 0x34 ~tb#stivS~ #8  // small portrait
	WRITE_ASCII 0x3c ~tb#stivM~ #8  // large portrait
	WRITE_ASCII 0x248 ~tb#stiv~ #8  // override script
	WRITE_ASCII 0x2cc ~tb#stiv~ #8  // pre-joining dialogue file
	WRITE_ASCII 0x280 ~tb#stiv~ #32 // DV
	
	ADD_CRE_ITEM ~sw1h08~ #1  #0 #0 ~IDENTIFIED~ ~WEAPON1~ EQUIP
	ADD_CRE_ITEM ~slng02~ #1  #0 #0 ~IDENTIFIED~ ~WEAPON2~
	ADD_CRE_ITEM ~shld08~ #1  #0 #0 ~IDENTIFIED~ ~SHIELD~
	ADD_CRE_ITEM ~leat02~ #1  #0 #0 ~IDENTIFIED~ ~ARMOR~
	ADD_CRE_ITEM ~bull01~ #40 #0 #0 ~IDENTIFIED~ ~QUIVER1~
	ADD_CRE_ITEM ~bull01~ #40 #0 #0 ~IDENTIFIED~ ~QUIVER2~
	ADD_CRE_ITEM ~bull02~ #5  #0 #0 ~IDENTIFIED~ ~QUIVER3~
	ADD_CRE_ITEM ~tb#surn~ #1 #0 #0 ~IDENTIFIED~ ~INV1~
	ADD_CRE_ITEM ~tb#spoo~ #2 #1 #0 ~IDENTIFIED~ ~QITEM1~
	
	SAY NAME1 @0
	SAY NAME2 @0
	SAY MORALE @1
	SAY HAPPY @2
	SAY UNHAPPY_ANNOYED @3
	SAY LEADER @4
	SAY TIRED @5
	SAY BATTLE_CRY1 @6
	SAY BATTLE_CRY2 @7
	SAY BATTLE_CRY3 @8
	SAY BATTLE_CRY4 @9
	SAY DAMAGE @10
	SAY DYING @11
	SAY HURT @12
	SAY AREA_FOREST @13
	SAY AREA_CITY @14
	SAY AREA_DUNGEON @15
	SAY AREA_DAY @16
	SAY AREA_NIGHT @17
	SAY SELECT_COMMON1 @18
	SAY SELECT_COMMON2 @19
	SAY SELECT_COMMON3 @20
	SAY SELECT_COMMON4 @21
	SAY SELECT_COMMON5 @22
	SAY SELECT_COMMON6 @23
	SAY SELECT_ACTION1 @24
	SAY SELECT_ACTION2 @25
	SAY SELECT_ACTION3 @26
	SAY SELECT_ACTION4 @27
	SAY SELECT_ACTION5 @28
	SAY SELECT_ACTION6 @29
	SAY SELECT_RARE1 @30
	SAY SELECT_RARE2 @31
	SAY CRITICAL_HIT @32
	SAY CRITICAL_MISS @33
	SAY TARGET_IMMUNE @34
	SAY INVENTORY_FULL @35
	SAY PICKED_POCKET @36
	SAY HIDDEN_IN_SHADOWS @37
	SAY SPELL_DISRUPTED @38
	SAY SET_A_TRAP @39
	SAY BIO @67
	

	
	
// Create new items	

COPY_EXISTING ~npsw02.itm~ ~override/tb#syk1.itm~
	SAY NAME1 @68
	SAY NAME2 @68
	WRITE_LONG 0x1e 0xfbbdfff2 // only usable by chaotic neutral halfling fighter/thieves and thieves
	SAY IDENTIFIED_DESC @69

COPY ~stivan/itm/tb#syk2.itm~ ~override~
	SAY NAME1 @70
	SAY NAME2 @70
	SAY IDENTIFIED_DESC @71
	WRITE_ASCII 0x3a tb#syk2i #8
	WRITE_ASCII 0x76 tb#syk2i #8

COPY_EXISTING ~misc01.itm~ ~override/tb#splu.itm~
	SAY NAME1 @72
	SAY NAME2 @72
	SAY IDENTIFIED_DESC @73
	SAY UNIDENTIFIED_DESC @73
	WRITE_ASCII 0x3a ~tb#splui~ #8 // inventory icon
	WRITE_LONG 0x34  1 // price
	WRITE_ASCII 0x44 ~gsack01~ #8 // ground icon
	WRITE_LONG  0x4c 0 // weight
	WRITE_ASCII 0x58 ~tb#spluc~ #8 // description icon

COPY_EXISTING ~misc01.itm~ ~override/tb#srpl.itm~
	SAY NAME1 @74
	SAY NAME2 @74
	SAY IDENTIFIED_DESC @75
	SAY UNIDENTIFIED_DESC @75
	WRITE_ASCII 0x3a ~tb#srpli~ #8 // inventory icon
	WRITE_LONG 0x34  1 // price
	WRITE_ASCII 0x44 ~gsack01~ #8 // ground icon
	WRITE_LONG  0x4c 0 // weight
	WRITE_ASCII 0x58 ~tb#srplc~ #8 // description icon


COPY_EXISTING ~misc01.itm~ ~override/tb#surn.itm~
	SAY NAME1 @76
	SAY NAME2 @76
	SAY IDENTIFIED_DESC @77
	SAY UNIDENTIFIED_DESC @77
	WRITE_LONG  0x18 ((THIS | 1) & 0xfffffffb)  // make unsellable and undroppable
	WRITE_ASCII 0x3a ~tb#surni~ #8 // inventory icon
	WRITE_ASCII 0x44 ~gsack01~ #8 // ground icon
	WRITE_LONG  0x4c 2 // weight
	WRITE_ASCII 0x58 ~tb#surnc~ #8 // description icon
	
COPY_EXISTING ~misc4w.itm~ ~override/tb#sdocu.itm~
	SAY NAME1 @78
	SAY NAME2 @78
	SAY IDENTIFIED_DESC @79
	SAY UNIDENTIFIED_DESC @79

COPY_EXISTING ~compon11.itm~ ~override/tb#spin.itm~  
  SAY NAME1 ~Spilla di Missy~
  SAY NAME2 ~Spilla di Missy~
  SAY IDENTIFIED_DESC ~Una semplice spilla.~
	
COPY_EXISTING ~stivan/itm/tb#spoo.itm~ ~override~
	SAY NAME1 @80
	SAY NAME2 @80
	SAY IDENTIFIED_DESC @81
	SAY UNIDENTIFIED_DESC @82
	WRITE_ASCII 0x3a ~tb#spooi~ #8 // inventory icon
	WRITE_ASCII 0x44 ~gsack01~ #8 // ground icon
	WRITE_LONG  0x4c 1 // weight
	WRITE_ASCII 0x58 ~cbag04~   #8 // description icon
	WRITE_ASCII 0x76 ~spwi106b~ #8 // header icon
	WRITE_ASCII 0xae ~spwi304b~ #8 // header icon



// Create PRO files	
	
COPY_EXISTING ~smllarea.pro~ ~override/tb#salc.pro~
	WRITE_BYTE 0x200 (THIS | 64) // make it area-friendly

ADD_PROJECTILE ~override/tb#salc.pro~	


// Copy SPL files

COPY ~stivan/spl/tb#sbli.spl~ ~override~

COPY ~stivan/spl/tb#salc.spl~ ~override~
	SAY 0x9e @83
	WRITE_SHORT   0x098 ~tb#salc~

COPY_EXISTING ~spwi905.spl~ ~override/tb#ssus1.spl~
  READ_LONG 0x64 "headoffset" ELSE 0
  READ_SHORT 0x68 "headcount" ELSE 0
  READ_LONG 0x6a "effectsoffset" ELSE 0
  WHILE ("%headcount%" > 0) BEGIN
    WRITE_SHORT ("%headoffset%" + (("%headcount%" - 1 ) * 0x28) + 0x12) 9
    READ_SHORT ("%headoffset%" + (("%headcount%" - 1 ) * 0x28) + 0x1e) "count" ELSE 0
    READ_SHORT ("%headoffset%" + (("%headcount%" - 1 ) * 0x28) + 0x20) "effectsindex" ELSE 0
    WHILE ("%count%" > 0) BEGIN
      READ_SHORT ("%effectsoffset%" + ("%effectsindex%" + "%count%" - 1 )* 0x30) OPCode ELSE 0
      PATCH_IF OPCode = 177 THEN BEGIN
        WRITE_ASCII ("%effectsoffset%" + ("%effectsindex%" + "%count%" - 1 )* 0x30 + 0x14) ~TB#SSUS1~ #8
      END
      SET "count" = ("%count%" -1)
    END
    SET "headcount" = "%headcount%" - 1
  END  
  
COPY_EXISTING ~spwi415.spl~ ~override/tb#ssus2.spl~
  READ_LONG 0x64 "headoffset" ELSE 0
  READ_SHORT 0x68 "headcount" ELSE 0
  READ_LONG 0x6a "effectsoffset" ELSE 0
  WHILE ("%headcount%" > 0) BEGIN
    READ_SHORT ("%headoffset%" + (("%headcount%" - 1 ) * 0x28) + 0x1e) "count" ELSE 0
    READ_SHORT ("%headoffset%" + (("%headcount%" - 1 ) * 0x28) + 0x20) "effectsindex" ELSE 0
    WHILE ("%count%" > 0) BEGIN
      WRITE_BYTE ("%effectsoffset%" + ("%effectsindex%" + "%count%" - 1 )* 0x30 + 0x0d) 0
      WRITE_BYTE ("%effectsoffset%" + ("%effectsindex%" + "%count%" - 1 )* 0x30 + 0x24) 0
      SET "count" = ("%count%" -1)
    END
    SET "headcount" = "%headcount%" - 1
  END


// Copy EFF files

COPY_EXISTING ~spgate.eff~ ~override/tb#ssus1.eff~
  WRITE_ASCII 0x30 tb#srat #8
  

// Patch 2DA files	

INCLUDE ~stivan/lib/tooltip.tpa~
LAF ~ADD_ITEM_TOOLTIPS~
  STR_VAR item     = ~tb#spoo~
          tooltips = ~@84 @83~
END

APPEND ~pdialog.2da~
~tb#stiv     tb#stivP            tb#stivJ           tb#stivD~
UNLESS ~tb#stiv~
UNLESS ~25POST~

APPEND ~pdialog.2da~
~tb#stiv     tb#stivP            tb#stivJ           tb#stivD        tb#st25P           tb#st25J              tb#st25D              tb#stiv25~
UNLESS ~tb#stiv~
IF     ~25POST~

APPEND ~interdia.2da~
~tb#stiv    tb#stivb~
UNLESS ~tb#stiv~
UNLESS ~25FILE~


APPEND ~interdia.2da~
~tb#stiv    tb#stivb       tb#st25b~
UNLESS ~tb#stiv~
IF     ~25FILE~


			  
// compile BAFs and Ds			  

APPEND ~STATE.IDS~ ~0x80101FEF CD_STATE_NOTVALID~ UNLESS ~CD_STATE_NOTVALID~

COMPILE ~stivan/compile~
COMPILE ~stivan/d/pid.d~ // must be last

ACTION_BASH_FOR ~stivan/baf~ ~^.*\.baf~ BEGIN
	EXTEND_TOP ~%BASH_FOR_RES%.bcs~ ~%BASH_FOR_FILESPEC%~
END


// Install ToB resources
ACTION_IF GAME_IS ~BGT ToB~ && 0 THEN BEGIN


	// Create PRO files

	COPY_EXISTING ~fireball.pro~ ~override/tb#sal2.pro~
		WRITE_BYTE 0x200 (THIS | 64) // make it area-friendly

	ADD_PROJECTILE ~override/tb#sal2.pro~	

	
	
	// Copy ITM files
	
	COPY_EXISTING ~stivan/tob/itm/tb#spoo2.itm~ ~override~
		SAY NAME1 @85
		SAY NAME2 @85
		SAY IDENTIFIED_DESC @86
		SAY UNIDENTIFIED_DESC @86
		WRITE_ASCII 0x3a ~tb#spo2i~ #8 // inventory icon
		WRITE_ASCII 0x44 ~gsack01~ #8 // ground icon
		WRITE_LONG  0x4c 1 // weight
		WRITE_ASCII 0x58 ~cbag04~   #8 // description icon
		WRITE_ASCII 0x76 ~spwi304b~ #8 // header icon
	
	
	
	// Copy SPL files
	
	COPY ~stivan/tob/spl/tb#sal2.spl~ ~override~
		SAY 0x9e @83
		WRITE_SHORT   0x09c ~tb#sal2~
		
	
	
	// Patch 2DA files
	
  LAF ~ADD_ITEM_TOOLTIPS~
    STR_VAR item     = ~tb#spoo2~
            tooltips = ~@83~
  END

END
