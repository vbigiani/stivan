ACTION_IF FILE_EXISTS ~override/tb#stiva.txt~ THEN BEGIN
FAIL ~Il mod e' gia' installato.~
END

ACTION_IF ! (GAME_IS ~BGT TOB~) THEN BEGIN
	FAIL ~E' richiesto Throne of Bhaal.~
END



// Clone misc resources


COPY_EXISTING ~uddrow35.dlg~ ~override/tb#sdr1.dlg~
		 ~uddrow35.dlg~ ~override/tb#sdr2.dlg~
		 ~uddrow35.dlg~ ~override/tb#sdr3.dlg~
	
COPY_EXISTING ~uddrow35.cre~ ~override~
	READ_ASCII 0x248 script
BUT_ONLY

COPY_EXISTING ~%script%.bcs~ ~override/tb#sdr1.bcs~
		~%script%.bcs~ ~override/tb#sdr2.bcs~
		~%script%.bcs~ ~override/tb#sdr3.bcs~

  
// Make sure that referenced scripts exist

COPY_EXISTING ~arnman04.cre~ ~override~
    ~pirpir10.cre~ ~override~
  READ_ASCII SCRIPT_OVERRIDE default_script
  PATCH_IF ~%default_script%~ STRING_COMPARE_CASE ~None~ &&
      ~%default_script%~ STRING_COMPARE_CASE ~~ &&
      ~%default_script%~ STRING_COMPARE_CASE ~%SOURCE_RES%~ THEN BEGIN
    PATCH_FAIL ~%SOURCE_RES%.cre has %default_script% as override script.~
  END
  WRITE_ASCIIE SCRIPT_OVERRIDE ~%SOURCE_RES%~ #8

COPY_EXISTING ~arnman04.cre~ ~override~
  ADD_CRE_ITEM ~tb#spin~ #1 #0 #0 ~none~ ~INV~

  

// copy static resources

COPY ~stivan/copy~ ~override~


// Add map notes

COPY_EXISTING ~ar0500.are~ ~override~
	ADD_MAP_NOTE #4845 #2049 gray ~Stivan, il Cacciatore~

		

// Create new CREs
	
COPY_EXISTING ~bhalf1.cre~ ~override~
	WRITE_ASCIIT 0x248 ~tb#stdes~

COPY_EXISTING ~seagul.cre~ ~override/tb#ssea.cre~
	WRITE_ASCII 0x280 ~tb#ssea~ #32
	
COPY_EXISTING ~uddrow35.cre~ ~override/tb#sdr1.cre~
		 ~uddrow35.cre~ ~override/tb#sdr2.cre~
		 ~uddrow35.cre~ ~override/tb#sdr3.cre~
	WRITE_EVALUATED_ASCII 0x248 ~%DEST_RES%~ #8 // override script
	WRITE_EVALUATED_ASCII 0x280 ~%DEST_RES%~ #32 // name
	WRITE_EVALUATED_ASCII 0x2cc ~%DEST_RES%~ #8 // dialogue

COPY_EXISTING ~heartg1.cre~ ~override~
  ADD_CRE_ITEM ~tb#sshi~ #1 #0 #0 ~none~ ~INV~
	
COPY_EXISTING ~wench1.cre~ ~override/tb#ssus.cre~
	SAY NAME1 ~Susan~
	SAY NAME2 ~Susan~
	WRITE_ASCII 0x248 ~shoutdlg~ #8 // override script
	WRITE_ASCII 0x250 ~NONE~ #8 // class script
	WRITE_ASCII 0x258 ~NONE~ #8 // race script
	WRITE_ASCII 0x260 ~NONE~ #8 // general script
	WRITE_ASCII 0x268 ~NONE~ #8 // default script
	WRITE_ASCII 0x280 ~tb#ssus~ #32 // name
	WRITE_ASCII 0x2cc ~tb#ssus~ #8 // dialogue
	
COPY_EXISTING ~nobl4.cre~ ~override/tb#scler.cre~
	SAY NAME1 ~Lawrence Cutterwere~
	SAY NAME2 ~Lawrence Cutterwere~
	WRITE_ASCII 0x248 ~NONE~ #8 // override script
	WRITE_ASCII 0x280 ~tb#scler~ #32 // name
	WRITE_ASCII 0x2cc ~tb#scler~ #8 // dialogue	

COPY_EXISTING ~noblew1.cre~ ~override/tb#stand.cre~
	SAY NAME1 ~Miss Tandalon~
	SAY NAME2 ~Miss Tandalon~
	WRITE_ASCII 0x248 ~tb#stand~ #8 // override script
	WRITE_ASCII 0x280 ~tb#stand~ #32 // name
	WRITE_ASCII 0x2cc ~tb#stand~ #8 // dialogue
  
COPY_EXISTING
    ~slumf1.cre~  ~override/tb#sros1.cre~
    ~slumm1.cre~  ~override/tb#sros2.cre~
    ~noblew1.cre~ ~override/tb#sros3.cre~
    ~noblem1.cre~ ~override/tb#sros4.cre~
	WRITE_ASCIIE 0x280 ~%DEST_RES%~ #32 // name
	WRITE_ASCIIE 0x2cc ~%DEST_RES%~ #8 // dialogue
  
COPY_EXISTING
    ~udogre.cre~  ~override/tb#scar.cre~
  SAY NAME1 ~Carlino~
  SAY NAME2 ~Carlino~
	WRITE_ASCIIE 0x280 ~%DEST_RES%~ #32 // name
	WRITE_ASCIIE 0x2cc ~%DEST_RES%~ #8 // dialogue
  REMOVE_CRE_ITEM ~minhp1~
  
COPY_EXISTING ~trthf01.cre~ ~override/tb#sros.cre~
  SAY NAME1 ~Rosalina~
  SAY NAME2 ~Rosalina~
	WRITE_ASCIIE 0x248 ~%DEST_RES%~ #8 // override script
	WRITE_ASCIIE 0x280 ~%DEST_RES%~ #32 // name
	WRITE_ASCIIE 0x2cc ~%DEST_RES%~ #8 // dialogue
  ADD_CRE_ITEM ~minhp1~ #1 #0 #0 ~none~ ~RING~

COPY_EXISTING ~naliames.cre~ ~override/tb#smess.cre~
	SAY NAME1 ~Messaggero~
	SAY NAME2 ~Messaggero~
	WRITE_ASCII 0x248 ~tb#smess~ #8 // override script
	WRITE_ASCII 0x280 ~tb#smess~ #32 // name
	WRITE_ASCII 0x2cc ~tb#smess~ #8 // dialogue
	
COPY_EXISTING ~c6elf1.cre~ ~override/tb#self.cre~	
	SAY NAME1 ~Elfo~
	SAY NAME2 ~Elfo~
	WRITE_ASCII 0x248 ~tb#self~ #8 // override script
	WRITE_ASCII 0x280 ~tb#self~ #32 // name
	WRITE_ASCII 0x2cc ~tb#self~ #8 // dialogue
	
COPY_EXISTING ~sword01.cre~ ~override/tb#slil.cre~
	SAY NAME1 ~Lilarcor~
	SAY NAME2 ~Lilarcor~
	WRITE_ASCII 0x248 ~tb#slil~ #8 // override script
	WRITE_ASCII 0x280 ~tb#slil~ #32 // name
	WRITE_ASCII 0x2cc ~tb#slil~ #8 // dialogue
	
COPY_EXISTING ~rat.cre~ ~override/tb#srat.cre~
  WRITE_ASCII 0x248 tb#stdes #8
  
COPY_EXISTING ~vamflm01.cre~ ~override/tb#svam1.cre~
  WRITE_ASCII 0x280 ~tb#svam1~ #32
  WRITE_ASCII 0x248 ~tb#svam1~ #8
  
COPY_EXISTING ~stivan/cre/tb#sskm.cre~ ~override/tb#sskm.cre~
	SAY NAME1 ~Scheletro Mago~
	SAY NAME2 ~Scheletro Mago~
	WRITE_ASCII 0x2cc ~tb#sskm~ #8
	
COPY_EXISTING ~stivan/cre/tb#sskc.cre~ ~override/tb#sskc.cre~
	SAY NAME1 ~Scheletro Chierico~
	SAY NAME2 ~Scheletro Chierico~
	WRITE_ASCII 0x2cc ~tb#sskc~ #8

COPY_EXISTING ~skelwa03.cre~ ~override/tb#sskf.cre~
	SAY NAME1 ~Scheletro Sergente~
	SAY NAME2 ~Scheletro Sergente~
	WRITE_LONG  0x14  8000 // XP value
	WRITE_SHORT 0x24  150  // HP
	WRITE_SHORT 0x26  150  // ditto
	WRITE_SHORT 0x46  0    // AC
	WRITE_SHORT 0x48  0    // ditto
	WRITE_BYTE  0x234 15   // level
	WRITE_BYTE  0x238 19   // strength
	WRITE_ASCII 0x2cc ~tb#sskf~ #8
	REPLACE_TEXTUALLY CASE_INSENSITIVE ~sw2h01~ ~sw2h11~ (8)
	REPLACE_TEXTUALLY CASE_INSENSITIVE ~bow01~ ~bow02~ (8)
	
COPY ~stivan/cre/tb#stiv.cre~ ~override~
	WRITE_LONG 0x14  0 // xp value
	WRITE_LONG 0x18  161000 // current XP
	WRITE_ASCII 0x34 ~tb#stivS~ #8  // small portrait
	WRITE_ASCII 0x3c ~tb#stivM~ #8  // large portrait
	WRITE_ASCII 0x248 ~tb#stiv~ #8  // override script
	WRITE_ASCII 0x2cc ~tb#stiv~ #8  // pre-joining dialogue file
	WRITE_ASCII 0x280 ~tb#stiv~ #32 // DV
	
	ADD_CRE_ITEM ~sw1h08~ #1  #0 #0 ~IDENTIFIED~ ~WEAPON1~ EQUIP
	ADD_CRE_ITEM ~slng02~ #1  #0 #0 ~IDENTIFIED~ ~WEAPON2~
	ADD_CRE_ITEM ~shld08~ #1  #0 #0 ~IDENTIFIED~ ~SHIELD~
	ADD_CRE_ITEM ~leat02~ #1  #0 #0 ~IDENTIFIED~ ~ARMOR~
	ADD_CRE_ITEM ~bull01~ #40 #0 #0 ~IDENTIFIED~ ~QUIVER1~
	ADD_CRE_ITEM ~bull01~ #40 #0 #0 ~IDENTIFIED~ ~QUIVER2~
	ADD_CRE_ITEM ~bull02~ #5  #0 #0 ~IDENTIFIED~ ~QUIVER3~
	ADD_CRE_ITEM ~tb#surn~ #1 #0 #0 ~IDENTIFIED~ ~INV1~
	ADD_CRE_ITEM ~tb#spoo~ #2 #1 #0 ~IDENTIFIED~ ~QITEM1~
	
	SAY NAME1 ~Stivan~
	SAY NAME2 ~Stivan~
	SAY MORALE ~Ahhhh! Vedo gabbiani OVUNQUE!~
	SAY HAPPY ~Questa sì che è una compagnia di manigoldi!~
	SAY UNHAPPY_ANNOYED ~Uhm... Mi sentivo più a mio agio quando vagabondavo per le strade di Athkatla.~
	SAY LEADER ~Io al comando? Wow!~
	SAY TIRED ~(Sbadiglia) Avrei un pò di sonno.~
	SAY BATTLE_CRY1 ~Cadrete per mano di Stivan il Cacciatore!~
	SAY BATTLE_CRY2 ~Possano i gabbiani pasteggiare con i vostri resti!~
	SAY BATTLE_CRY3 ~Heh. Mi chiedevo quando ci saremmo dati da fare.~
	SAY BATTLE_CRY4 ~Yahoo! All'attacco!~
	SAY DAMAGE ~Auch!~
	SAY DYING ~(Gemito)~
	SAY HURT ~(Gasp!) Sto... Sto morendo...!~
	SAY AREA_FOREST ~(Starnutisce) Fantastico. Questo sarà il primo di una lunga serie. Io ODIO la natura!~
	SAY AREA_CITY ~Ahh... Mi sento sempre a mio agio tra queste masse brulicanti di gente.~
	SAY AREA_DUNGEON ~Uhm... Fà piuttosto freddino qui.~
	SAY AREA_DAY ~Per quanto luminosa, la luce del sole non eguaglia in bellezza quella delle stelle e della luna.~
	SAY AREA_NIGHT ~Le stelle sono buchi nel cielo da cui... da cui... *Ahem* Che buio!~
	SAY SELECT_COMMON1 ~Yeah.~
	SAY SELECT_COMMON2 ~Cosa vuoi?~
	SAY SELECT_COMMON3 ~Non c'è due senza tre, suppongo.~
	SAY SELECT_COMMON4 ~Uhm?~
	SAY SELECT_COMMON5 ~Nessuno fa qualcosa per niente.~
	SAY SELECT_COMMON6 ~E' il mio momento!~
	SAY SELECT_ACTION1 ~Sicuro.~
	SAY SELECT_ACTION2 ~Vallo a prendere te!~
	SAY SELECT_ACTION3 ~Heh. Me l'aspettavo.~
	SAY SELECT_ACTION4 ~Vado e torno.~
	SAY SELECT_ACTION5 ~D'accordo, d'accordo... Ci penso io.~
	SAY SELECT_ACTION6 ~Facile come spennare un gabbiano.~
	SAY SELECT_RARE1 ~Se devo.~
	SAY SELECT_RARE2 ~E' tutto?~
	SAY CRITICAL_HIT ~Haha!~
	SAY CRITICAL_MISS ~Oops.~
	SAY TARGET_IMMUNE ~Ci vuole qualcos'altro qui!~
	SAY INVENTORY_FULL ~Per cosa mi hai preso, per uno sgabuzzino?~
	SAY PICKED_POCKET ~Ho rubacchiato qualcosina.~
	SAY HIDDEN_IN_SHADOWS ~Heh. Sfido chiunque a vedermi.~
	SAY SPELL_DISRUPTED ~Oh, no! Ho perso concentrazione!~
	SAY SET_A_TRAP ~Heh. Se aggiungessi un pò di cacca di gabbiano, questa trappola sarebbe ancora più micidiale.~
	SAY BIO ~Se interrogato sul suo passato, STIVAN non esita a lanciarsi in un corposo riassunto della sua vita. Nativo di Athkatla e figlio di un mercante di vini, l'halfling si è visto uccidere i propri genitori e i fratelli da uno stormo inferocito di gabbiani a cui è miracolosamente sfuggito. Da qui il suo odio feroce per quello che definisce "il nemico pennuto", a sua detta responsabile anche delle disgrazie che seguirono - la perdita dell'attività del padre e di tutti i suoi beni per opera di un ricco mercante che, invece di aiutarlo, lo portò alla bancarotta.
	Costretto a vivere per le strade di Athkatla, l'halfling ha accolto con entusiasmo l'offerta di <CHARNAME> di unirsi alla sua compagnia, felice di mettere a disposizione la sua conoscenza della città e di aver trovato un nuovo alleato nella sua campagna contro i gabbiani.~
	

	
	
// Create new items	

COPY_EXISTING ~npsw02.itm~ ~override/tb#syk1.itm~
	SAY NAME1 ~Vendetta di Yoshimo +1~
	SAY NAME2 ~Vendetta di Yoshimo +1~
	WRITE_LONG 0x1e 0xfbbdfff2 // only usable by chaotic neutral halfling fighter/thieves and thieves
	SAY IDENTIFIED_DESC ~Questa katana apparteneva a Yoshimo, il cacciatore di taglie di Kara-Tur legato ad Irenicus da una costrizione risultata fatale. L'incantesimo che la rendeva utilizzabile soltanto da lui è stato rimosso in seguito ad una generosa donazione ad Ilmater, a condizione però che l'arma venga utilizzata contro il responsabile della morte del ladro.
	
Stivan spruzza entusiasmo da tutti i pori quando brandisce quest'arma, entusiasmo che i tuoi compagni poco apprezzano quando la lama arriva pericolosamente vicino alle loro teste.

STATISTICHE:

THAC0: Bonus +1
Danno: 1D10 +1
Tipo di Danno: Taglio
Peso: 5
Fattore Velocità: 3
Tipo di Competenza: Katana
Tipo: A una mano
Richiede: Forza 6
Utilizzabile Da:
Stivan~

COPY ~stivan/itm/tb#syk2.itm~ ~override~
	SAY NAME1 ~Vendetta di Yoshimo +3~
	SAY NAME2 ~Vendetta di Yoshimo +3~
	SAY IDENTIFIED_DESC ~Questa katana apparteneva a Yoshimo, il cacciatore di taglie di Kara-Tur legato ad Irenicus da una costrizione risultata fatale. L'incantesimo che la rendeva utilizzabile soltanto da lui è stato rimosso in seguito ad una generosa donazione ad Ilmater, a condizione però che l'arma venga utilizzata contro il responsabile della morte del ladro.

Stivan spruzza entusiasmo da tutti i pori quando brandisce quest'arma, entusiasmo che i tuoi compagni poco apprezzano quando la lama arriva pericolosamente vicino alle loro teste.

Al fine di rendere la lama più temibile, il fabbro Cromwell ne ha potenziato l'originario incantamento e vi ha infuso le proprietà dell'oggetto da cui Stivan non si separerebbe mai -- la cacca di gabbiano. Sorprendentemente, l'halfling si è guardato bene dal dirgli cosa fosse quella "strana poltiglia biancastra". 

STATISTICHE:

Abilità in Combattimento:
10% di possibilità che il bersaglio venga accecato per due round ad ogni colpo messo a segno (a meno che non superi un tiro salvezza contro soffio con penalità -2);
5% di possibilità che il bersaglio venga investito da un'Esplosione Alchemica (4d4 danni da fuoco addizionali, nessun tiro salvezza).

THAC0: Bonus +3
Danno: 1D10 +3
Tipo di Danno: Taglio
Peso: 5
Fattore Velocità: 1
Tipo di Competenza: Katana
Tipo: A una mano
Richiede: Forza 6
Utilizzabile Da:
Stivan~
	WRITE_ASCII 0x3a tb#syk2i #8
	WRITE_ASCII 0x76 tb#syk2i #8

COPY_EXISTING ~misc01.itm~ ~override/tb#splu.itm~
	SAY NAME1 ~Piuma~
	SAY NAME2 ~Piuma~
	SAY IDENTIFIED_DESC ~Una piuma di Gabbiano.~
	SAY UNIDENTIFIED_DESC ~Una piuma di Gabbiano.~
	WRITE_ASCII 0x3a ~tb#splui~ #8 // inventory icon
	WRITE_LONG 0x34  1 // price
	WRITE_ASCII 0x44 ~gsack01~ #8 // ground icon
	WRITE_LONG  0x4c 0 // weight
	WRITE_ASCII 0x58 ~tb#spluc~ #8 // description icon

COPY_EXISTING ~misc57.itm~ ~override/tb#sshi.itm~
  SAY NAME1 ~Lo scudo del Mancino~
  SAY NAME2 ~Lo scudo del Mancino~
  SAY IDENTIFIED_DESC ~Vi era un tempo in cui Tyr non era conosciuto con l'appellativo di "Il Monco". La maggiore tra le divinità della Triade, infatti, perse la mano destra nello scontro con Kezef il Segugio del Caos, ma prima di allora era solito accompagnare la sua fatale arma con questo scudo. La leggenda vuole che Tyr l'abbia tenuto ugualmente con sè sino al Periodo dei Disordini, quando decise di donarlo ai suoi seguaci di Cormyr per essere rimasti al lui fedeli anche dopo la cecità inflittagli da Lord Ao.
Sebbene sia sempre stato custodito dai paladini e dai chierici devoti al dio, lo scudo in questione viene utilizzato ogni anno in occasione di un rito propiziatorio al raccolto e alla lustrazione dei campi. Il culto di Tyr è infatti molto diffuso nelle campagne, dove viene chiamato dai più "Dio Pannocchia".

Realizzato in acciaio pesante con venature d'argento, l'artefatto pesa così tanto da essere un impedimento più che una protezione in battaglia. Pare che sino ad oggi soltanto Tyr sia stato in grado di brandirlo, tant'è che è necessaria ben più di una persona per trasportarlo.

Peso: 100~
  SAY UNIDENTIFIED_DESC ~Vi era un tempo in cui Tyr non era conosciuto con l'appellativo di "Il Monco". La maggiore tra le divinità della Triade, infatti, perse la mano destra nello scontro con Kezef il Segugio del Caos, ma prima di allora era solito accompagnare la sua fatale arma con questo scudo. La leggenda vuole che Tyr l'abbia tenuto ugualmente con sè sino al Periodo dei Disordini, quando decise di donarlo ai suoi seguaci di Cormyr per essere rimasti al lui fedeli anche dopo la cecità inflittagli da Lord Ao.
Sebbene sia sempre stato custodito dai paladini e dai chierici devoti al dio, lo scudo in questione viene utilizzato ogni anno in occasione di un rito propiziatorio al raccolto e alla lustrazione dei campi. Il culto di Tyr è infatti molto diffuso nelle campagne, dove viene chiamato dai più "Dio Pannocchia".

Realizzato in acciaio pesante con venature d'argento, l'artefatto pesa così tanto da essere un impedimento più che una protezione in battaglia. Pare che sino ad oggi soltanto Tyr sia stato in grado di brandirlo, tant'è che è necessaria ben più di una persona per trasportarlo.

Peso: 100~
  WRITE_LONG 0x4c 100
  WRITE_LONG 0x18 THIS | BIT0
  
COPY_EXISTING ~misc01.itm~ ~override/tb#srpl.itm~
	SAY NAME1 ~Piuma Rossa~
	SAY NAME2 ~Piuma Rossa~
	SAY IDENTIFIED_DESC ~Questa piuma rossa è un regalo datoti da Stivan come segno della sua gratitudine. L’halfling la descrive come il suo "più prezioso tesoro", e probabilmente è per questo motivo che ad ogni sosta si accerta che sia sempre nel tuo zaino.~
	SAY UNIDENTIFIED_DESC ~Questa piuma rossa è un regalo datoti da Stivan come segno della sua gratitudine. L’halfling la descrive come il suo "più prezioso tesoro", e probabilmente è per questo motivo che ad ogni sosta si accerta che sia sempre nel tuo zaino.~
	WRITE_ASCII 0x3a ~tb#srpli~ #8 // inventory icon
	WRITE_LONG 0x34  1 // price
	WRITE_ASCII 0x44 ~gsack01~ #8 // ground icon
	WRITE_LONG  0x4c 0 // weight
	WRITE_ASCII 0x58 ~tb#srplc~ #8 // description icon


COPY_EXISTING ~misc01.itm~ ~override/tb#surn.itm~
	SAY NAME1 ~Urna di Stivan~
	SAY NAME2 ~Urna di Stivan~
	SAY IDENTIFIED_DESC ~Questa è l'urna contenente le ceneri dei familiari di Stivan. Guardandola più da vicino, ti accorgi che presenta diversi graffi lungo la superficie e che il colore è diventato opaco in alcuni punti.
Temendo che possa rompersi nel corso di un combattimento, l'halfling ti ha chiesto di rivolgerti ad uno degli impiegati nel Palazzo di Giustizia per comprare una tomba dove riporla al sicuro.~
	SAY UNIDENTIFIED_DESC ~Questa è l'urna contenente le ceneri dei familiari di Stivan. Guardandola più da vicino, ti accorgi che presenta diversi graffi lungo la superficie e che il colore è diventato opaco in alcuni punti.
Temendo che possa rompersi nel corso di un combattimento, l'halfling ti ha chiesto di rivolgerti ad uno degli impiegati nel Palazzo di Giustizia per comprare una tomba dove riporla al sicuro.~
	WRITE_LONG  0x18 ((THIS | 1) & 0xfffffffb)  // make unsellable and undroppable
	WRITE_ASCII 0x3a ~tb#surni~ #8 // inventory icon
	WRITE_ASCII 0x44 ~gsack01~ #8 // ground icon
	WRITE_LONG  0x4c 2 // weight
	WRITE_ASCII 0x58 ~tb#surnc~ #8 // description icon
	
COPY_EXISTING ~misc4w.itm~ ~override/tb#sdocu.itm~
	SAY NAME1 ~Documenti~
	SAY NAME2 ~Documenti~
	SAY IDENTIFIED_DESC ~Non immaginavi che per legittimare l’acquisizione della cripta da parte di Stivan fossero necessari così tanti documenti. Non puoi fare a meno di chiederti se questa mole di atti, firme e sigilli dipendano dall’apparato burocratico di Athkatla o dall’eccessiva minuziosità dell’impiegato Lawrence.~
	SAY UNIDENTIFIED_DESC ~Non immaginavi che per legittimare l’acquisizione della cripta da parte di Stivan fossero necessari così tanti documenti. Non puoi fare a meno di chiederti se questa mole di atti, firme e sigilli dipendano dall’apparato burocratico di Athkatla o dall’eccessiva minuziosità dell’impiegato Lawrence.~

COPY_EXISTING ~compon11.itm~ ~override/tb#spin.itm~  
  SAY NAME1 ~Spilla di Missy~
  SAY NAME2 ~Spilla di Missy~
  SAY IDENTIFIED_DESC ~Una semplice spilla.~
	
COPY_EXISTING ~stivan/itm/tb#spoo.itm~ ~override~
	SAY NAME1 ~Cacca di gabbiano~
	SAY NAME2 ~Cacca di gabbiano~
	SAY IDENTIFIED_DESC ~Questo sacchetto contiene una certa quantità di cacca di gabbiano raccolta da Stivan. L'halfling è solito servirsene per accecare i propri avversari o per creare una miscela alchemica che esplode se lanciata contro un nemico. Preferisci non indagare su come faccia a non esaurire mai le scorte.
	
STATISTICHE:

Lancia "Cecità" due volte al giorno: il bersaglio deve superare un tiro salvezza contro soffio (penalità -2) o restare accecato per due round.
Lancia "Miscela alchemica" una volta al giorno: infligge 4d4 danni da fuoco (nessun tiro salvezza).

Utilizzabile Da:
Stivan~
	SAY UNIDENTIFIED_DESC ~Questo sacchetto contiene una certa quantità di cacca di gabbiano raccolta da Stivan. L'halfling è solito servirsene per accecare i propri avversari o per creare una miscela alchemica che esplode se lanciata contro un nemico. Preferisci non indagare su come faccia a non esaurire mai le scorte.

STATISTICHE:

Lancia "Cecità" due volte al giorno: il bersaglio deve superare un tiro salvezza contro soffio (penalità -2) o restare accecato per due round.
Lancia "Miscela alchemica" una volta al giorno: infligge 4d4 danni da fuoco (nessun tiro salvezza).

Utilizzabile Da:
Stivan~
	WRITE_ASCII 0x3a ~tb#spooi~ #8 // inventory icon
	WRITE_ASCII 0x44 ~gsack01~ #8 // ground icon
	WRITE_LONG  0x4c 1 // weight
	WRITE_ASCII 0x58 ~cbag04~   #8 // description icon
	WRITE_ASCII 0x76 ~spwi106b~ #8 // header icon
	WRITE_ASCII 0xae ~spwi304b~ #8 // header icon



// Create PRO files	
	
COPY_EXISTING ~smllarea.pro~ ~override/tb#salc.pro~
	WRITE_BYTE 0x200 (THIS | 64) // make it area-friendly

ADD_PROJECTILE ~override/tb#salc.pro~	


// Copy SPL files

COPY ~stivan/spl/tb#sbli.spl~ ~override~

COPY ~stivan/spl/tb#salc.spl~ ~override~
	SAY 0x9e ~Esplosione Alchemica~
	WRITE_SHORT   0x098 ~tb#salc~

COPY_EXISTING ~spwi905.spl~ ~override/tb#ssus1.spl~
  READ_LONG 0x64 "headoffset" ELSE 0
  READ_SHORT 0x68 "headcount" ELSE 0
  READ_LONG 0x6a "effectsoffset" ELSE 0
  WHILE ("%headcount%" > 0) BEGIN
    WRITE_SHORT ("%headoffset%" + (("%headcount%" - 1 ) * 0x28) + 0x12) 9
    READ_SHORT ("%headoffset%" + (("%headcount%" - 1 ) * 0x28) + 0x1e) "count" ELSE 0
    READ_SHORT ("%headoffset%" + (("%headcount%" - 1 ) * 0x28) + 0x20) "effectsindex" ELSE 0
    WHILE ("%count%" > 0) BEGIN
      READ_SHORT ("%effectsoffset%" + ("%effectsindex%" + "%count%" - 1 )* 0x30) OPCode ELSE 0
      PATCH_IF OPCode = 177 THEN BEGIN
        WRITE_ASCII ("%effectsoffset%" + ("%effectsindex%" + "%count%" - 1 )* 0x30 + 0x14) ~TB#SSUS1~ #8
      END
      SET "count" = ("%count%" -1)
    END
    SET "headcount" = "%headcount%" - 1
  END  
  
COPY_EXISTING ~spwi415.spl~ ~override/tb#ssus2.spl~
  READ_LONG 0x64 "headoffset" ELSE 0
  READ_SHORT 0x68 "headcount" ELSE 0
  READ_LONG 0x6a "effectsoffset" ELSE 0
  WHILE ("%headcount%" > 0) BEGIN
    READ_SHORT ("%headoffset%" + (("%headcount%" - 1 ) * 0x28) + 0x1e) "count" ELSE 0
    READ_SHORT ("%headoffset%" + (("%headcount%" - 1 ) * 0x28) + 0x20) "effectsindex" ELSE 0
    WHILE ("%count%" > 0) BEGIN
      WRITE_BYTE ("%effectsoffset%" + ("%effectsindex%" + "%count%" - 1 )* 0x30 + 0x0d) 0
      WRITE_BYTE ("%effectsoffset%" + ("%effectsindex%" + "%count%" - 1 )* 0x30 + 0x24) 0
      SET "count" = ("%count%" -1)
    END
    SET "headcount" = "%headcount%" - 1
  END


// Copy EFF files

COPY_EXISTING ~spgate.eff~ ~override/tb#ssus1.eff~
  WRITE_ASCII 0x30 tb#srat #8
  

// Patch 2DA files	

INCLUDE ~stivan/lib/tooltip.tpa~
LAF ~ADD_ITEM_TOOLTIPS~
  STR_VAR item     = ~tb#spoo~
          tooltips = ~99999999 99999998~
END

COPY_EXISTING ~tooltip.2da~ ~override~
REPLACE ~99999999~ ~Cecità~
REPLACE ~99999998~ ~Esplosione Alchemica~


APPEND ~pdialog.2da~
~tb#stiv     tb#stivP            tb#stivJ           tb#stivD~
UNLESS ~tb#stiv~
UNLESS ~25POST~

APPEND ~pdialog.2da~
~tb#stiv     tb#stivP            tb#stivJ           tb#stivD        tb#st25P           tb#st25J              tb#st25D              tb#stiv25~
UNLESS ~tb#stiv~
IF     ~25POST~

APPEND ~interdia.2da~
~tb#stiv    tb#stivb~
UNLESS ~tb#stiv~
UNLESS ~25FILE~


APPEND ~interdia.2da~
~tb#stiv    tb#stivb       tb#st25b~
UNLESS ~tb#stiv~
IF     ~25FILE~


			  
// compile BAFs and Ds			  

APPEND ~STATE.IDS~ ~0x80101FEF CD_STATE_NOTVALID~ UNLESS ~CD_STATE_NOTVALID~

COMPILE ~stivan/compile~
COMPILE ~stivan/d/pid.d~ // must be last

ACTION_BASH_FOR ~stivan/baf~ ~^.*\.baf~ BEGIN
	EXTEND_BOTTOM ~%BASH_FOR_RES%.bcs~ ~%BASH_FOR_FILESPEC%~
END


// Install ToB resources
ACTION_IF GAME_IS ~BGT ToB~ && 0 THEN BEGIN


	// Create PRO files

	COPY_EXISTING ~fireball.pro~ ~override/tb#sal2.pro~
		WRITE_BYTE 0x200 (THIS | 64) // make it area-friendly

	ADD_PROJECTILE ~override/tb#sal2.pro~	

	
	
	// Copy ITM files
	
	COPY_EXISTING ~stivan/tob/itm/tb#spoo2.itm~ ~override~
		SAY NAME1 ~Cacca di gabbiano infuocata~
		SAY NAME2 ~Cacca di gabbiano infuocata~
		SAY IDENTIFIED_DESC ~Questo sacchetto contiene una certa quantità di cacca di gabbiano raccolta da Stivan. Dopo essere stata combinata con la piuma di fenice e la barba di Yaga-shura, genera un'esplosione alchemica piu' potente, ma non e' piu' in grado di accecare i nemici.
STATISTICHE:
Lancia "Miscela alchemica" una volta al giorno: infligge 8d8 danni da fuoco (nessun tiro salvezza).

Utilizzabile Da:
Stivan~
		SAY UNIDENTIFIED_DESC ~Questo sacchetto contiene una certa quantità di cacca di gabbiano raccolta da Stivan. Dopo essere stata combinata con la piuma di fenice e la barba di Yaga-shura, genera un'esplosione alchemica piu' potente, ma non e' piu' in grado di accecare i nemici.
STATISTICHE:
Lancia "Miscela alchemica" una volta al giorno: infligge 8d8 danni da fuoco (nessun tiro salvezza).

Utilizzabile Da:
Stivan~
		WRITE_ASCII 0x3a ~tb#spo2i~ #8 // inventory icon
		WRITE_ASCII 0x44 ~gsack01~ #8 // ground icon
		WRITE_LONG  0x4c 1 // weight
		WRITE_ASCII 0x58 ~cbag04~   #8 // description icon
		WRITE_ASCII 0x76 ~spwi304b~ #8 // header icon
	
	
	
	// Copy SPL files
	
	COPY ~stivan/tob/spl/tb#sal2.spl~ ~override~
		SAY 0x9e ~Esplosione Alchemica~
		WRITE_SHORT   0x09c ~tb#sal2~
		
	
	
	// Patch 2DA files
	
  LAF ~ADD_ITEM_TOOLTIPS~
    STR_VAR item     = ~tb#spoo2~
            tooltips = ~99999999~
  END

  COPY_EXISTING ~tooltip.2da~ ~override~
  REPLACE ~99999999~ ~Esplosione Alchemica~

END
