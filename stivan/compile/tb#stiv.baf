//////////////////////////////////////////////////////////////
// NPC death reaction
//////////////////////////////////////////////////////////////

IF
	Dead("Keldorn")
	InPartyAllowDead("Keldorn")
	Global("tb#KeldornDead","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("tb#KeldornDead","GLOBAL",1)
		DisplayStringHead(Myself,~[Nascondendo una risata] Poveraccio, se n'è andato!~)
END
IF
	!Dead("Keldorn")
	Global("tb#KeldornDead","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("tb#KeldornDead","GLOBAL",0)
END

IF
	Dead("Aerie")
	InPartyAllowDead("Aerie")
	Global("tb#AerieDead","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("tb#AerieDead","GLOBAL",1)
		DisplayStringHead(Myself,~[Nascondendo una risata] Poveraccia, se n'è andata!~)
END
IF
	!Dead("Aerie")
	Global("tb#AerieDead","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("tb#AerieDead","GLOBAL",0)
END

IF
	Dead("Valygar")
	InPartyAllowDead("Valygar")
	Global("tb#ValygarDead","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("tb#ValygarDead","GLOBAL",1)
		DisplayStringHead(Myself,~[Nascondendo una risata] Poveraccio, se n'è andato!~)
END
IF
	!Dead("Valygar")
	Global("tb#ValygarDead","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("tb#ValygarDead","GLOBAL",0)
END

IF
	Dead("Anomen")
	InPartyAllowDead("Anomen")
	Global("tb#AnomenDead","GLOBAL",0)
	Global("AnomenIsNotKnight","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("tb#AnomenDead","GLOBAL",0)
		DisplayStringHead(Myself,~[Nascondendo una risata] Poveraccio, se n'è andato!~)
END
IF
	Dead("Anomen")
	InPartyAllowDead("Anomen")
	Global("tb#AnomenDead","GLOBAL",0)
	Global("AnomenIsNotKnight","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("tb#AnomenDead","GLOBAL",1)
		DisplayStringHead(Myself,~nnnnNNNNOOOOOO!!!~)
END
IF
	!Dead("Anomen")
	Global("tb#AnomenDead","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("tb#AnomenDead","GLOBAL",0)
END

IF
	Dead("Nalia")
	InPartyAllowDead("Nalia")
	Global("tb#NaliaDead","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("tb#NaliaDead","GLOBAL",1)
		DisplayStringHead(Myself,~nnnnNNNNOOOOOO!!!~)
END
IF
	!Dead("Nalia")
	Global("tb#NaliaDead","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("tb#NaliaDead","GLOBAL",0)
END

IF
	Dead("Yoshimo")
	InPartyAllowDead("Yoshimo")
	Global("tb#YoshimoDead","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("tb#YoshimoDead","GLOBAL",1)
		DisplayStringHead(Myself,~nnnnNNNNOOOOOO!!!~)
END
IF
	!Dead("Yoshimo")
	Global("tb#YoshimoDead","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("tb#YoshimoDead","GLOBAL",0)
END

IF
	Dead("Viconia")
	InPartyAllowDead("Viconia")
	Global("tb#ViconiaDead","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("tb#ViconiaDead","GLOBAL",1)
		DisplayStringHead(Myself,~nnnnNNNNOOOOOO!!!~)
END
IF
	!Dead("Viconia")
	Global("tb#ViconiaDead","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("tb#ViconiaDead","GLOBAL",0)
END

//////////////////////////////////////////////////////////////
// Urn quest
// Variables:
//   tb#stivanEntryToggle, 1 if the party has the urn, 0 otherwise
//   tb#stivanEntrytimer, quest timer, only works if the party has the urn
//   tb#stivanMessageTimer \ to make a messenger appear
//   tb#stivanMessage      /
//   tb#stivanEntryTest, state tracker:
//        0: Stivan never joined
//        1: quest started
//        2: talked to Lawrence
//        3: Lawrence has the urn
//        4: talked with the messenger
//        5: party has the urn & documents
//        6: urn is safe
//        7: killed undead, quest complete.
//////////////////////////////////////////////////////////////
IF
	Global("tb#stivanEntryToggle","GLOBAL",0)
	!PartyHasItem("tb#surn")
THEN
	RESPONSE #100
		SetGlobal("tb#stivanEntryToggle","GLOBAL",1)
END

IF
	Global("tb#stivanEntryToggle","GLOBAL",1)
	PartyHasItem("tb#surn")
THEN
	RESPONSE #100
		SetGlobal("tb#stivanEntryToggle","GLOBAL",0)
		SetGlobalTimer("tb#stivanEntryTimer","GLOBAL",FOUR_DAYS)
END

IF
	Global("tb#stivanEntryToggle","GLOBAL",0)
	GlobalTimerExpired("tb#stivanEntryTimer","GLOBAL")
	CombatCounterLT(1)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	PartyHasItem("tb#surn")
	OR(2)
		Global("tb#UrnBroken","GLOBAL",0)
		Global("tb#UrnBroken","GLOBAL",2)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		IncrementGlobal("tb#UrnBroken","GLOBAL",1)
		StartDialogueNoSet(Player1)
		SetInterrupt(TRUE)
END

IF
	!GlobalTimerExpired("tb#stivanMessageTimer","GLOBAL")
	Global("tb#stivanEntryTest","GLOBAL",3)
	GlobalGT("Chapter","GLOBAL",5)
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	Global("tb#stivMessage","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobalTimer("tb#stivanMessageTimer","GLOBAL",TWO_DAYS)
END

IF
	GlobalTimerExpired("tb#stivanMessageTimer","GLOBAL")
	Global("tb#stivanEntryTest","GLOBAL",3)
	GlobalGT("Chapter","GLOBAL",5)
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	Global("tb#stivMessage","GLOBAL",0)
	AreaType(OUTDOOR)
THEN
	RESPONSE #100
		SetGlobal("tb#stivMessage","GLOBAL",1)
		CreateCreatureOffscreen("tb#smess",0)
END

//////////////////////////////////////////////////////////////
// Yoshimo betrayal dialoogue
//////////////////////////////////////////////////////////////

IF
	InParty("tb#stiv")
	InParty("Yoshimo")
	!StateCheck("tb#stiv",CD_STATE_NOTVALID)
	!StateCheck("Yoshimo",CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("Chapter","GLOBAL",4)
	CombatCounter(0)
	Global("tb#BeforeYoshiBetrayal","GLOBAL",0)
	!RealGlobalTimerNotExpired("tb#StivanNoSpam","GLOBAL")
THEN
	RESPONSE #80
		RealSetGlobalTimer("tb#StivanNoSpam","GLOBAL",20)
	RESPONSE #20
		RealSetGlobalTimer("tb#StivanNoSpam","GLOBAL",200)
		StartDialogueNoSet(Player1)
END

IF
	InParty("tb#stiv")
	!StateCheck("tb#stiv",CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	Global("tb#AfterYoshiBetrayal","GLOBAL",1)
	!RealGlobalTimerNotExpired("tb#StivanNoSpam","GLOBAL")
THEN
	RESPONSE #80
		RealSetGlobalTimer("tb#StivanNoSpam","GLOBAL",20)
	RESPONSE #20
		RealSetGlobalTimer("tb#StivanNoSpam","GLOBAL",200)
		StartDialogueNoSet(Player1)
END

//////////////////////////////////////////////////////////////
// Misc scripted dialogues
//////////////////////////////////////////////////////////////

IF
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	AreaCheck("AR0502")
	Global("tb#StivanEnteredTanner2","AR0502",0)
THEN
	RESPONSE #100
		StartDialogueNoSet(Player1)
END

IF
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!RealGlobalTimerExpired("tb#StivanNoSpam","GLOBAL")
	!RealGlobalTimerNotExpired("tb#StivanNoSpam","GLOBAL")
THEN
	RESPONSE #100
		RealSetGlobalTimer("tb#StivanNoSpam","GLOBAL",200)
END

IF
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	Global("tb#StivanPCTalk","GLOBAL",0)
THEN
	RESPONSE #80
		RealSetGlobalTimer("tb#StivanNoSpam","GLOBAL",20)
	RESPONSE #20
		RealSetGlobalTimer("tb#StivanNoSpam","GLOBAL",200)
		StartDialogueNoSet(Player1)
END

	

IF
	PartyRested()
	InParty("Viconia")
	See("Viconia")
	!StateCheck("Viconia",CD_STATE_NOTVALID)
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	CombatCounter(0)
	Global("tb#StivanViconia","GLOBAL",1)
THEN
	RESPONSE #100
		StartDialogueNoSet("Viconia")
END

IF
	InParty("Anomen")
	See("Anomen")
	!StateCheck("Anomen",CD_STATE_NOTVALID)
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	Global("AnomenIsNotKnight","GLOBAL",0)
	OR(2)
	AreaCheck("AR0901")
	AreaCheck("AR0902")
	CombatCounter(0)
	Global("tb#StivanAnomenAboutFaith","GLOBAL",0)
THEN
	RESPONSE #100
		StartDialogueNoSet("Anomen")
END

IF
	Global("TB#StivanSusanScene","GLOBAL",1)
THEN
	RESPONSE #100
		StartCutSceneMode()
		SetGlobal("TB#StivanSusanScene","GLOBAL",2)
		SetSequence(SEQ_CAST)
		Wait(1)
		CreateCreature("tb#ssus",[-1.-1],0)
		ActionOverride("tb#stiv",ReallyForceSpell("tb#ssus",FLASHY_1))
		SetSequence(SEQ_READY)
		Wait(1)
		ActionOverride("tb#ssus",StartDialogueNoSet("Edwin"))
END

IF
	Global("TB#StivanSusanScene","GLOBAL",3)
THEN
	RESPONSE #100
		StartCutSceneMode()
		SetGlobal("TB#StivanSusanScene","GLOBAL",4)
		ActionOverride("Edwin",SetSequence(SEQ_CAST))
		Wait(1)
		ActionOverride("tb#stiv",ReallyForceSpell("tb#ssus",FLASHY_1))
		ActionOverride("tb#ssus",Polymorph(RAT))
		ActionOverride("Edwin",SetSequence(SEQ_READY))
		Wait(1)
		ActionOverride("Edwin",StartDialogueNoSet("tb#stiv"))
END

IF
	InParty("Haerdalis")
	See("Haerdalis")
	!StateCheck("Haerdalis",CD_STATE_NOTVALID)
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	AreaType(OUTDOOR)
	CombatCounter(0)
	Global("tb#StivanHaerdalisBesamen","GLOBAL",0)
	OR(2)
	Global("BesamenDead","GLOBAL",1)
	Global("BesamenDead","GLOBAL",2)
	!RealGlobalTimerNotExpired("tb#StivanNoSpam","GLOBAL")
THEN
	RESPONSE #80
		RealSetGlobalTimer("tb#StivanNoSpam","GLOBAL",20)
	RESPONSE #20
		RealSetGlobalTimer("tb#StivanNoSpam","GLOBAL",200)
		StartDialogueNoSet("heardalis")
END

IF
	InParty("Jaheira")
	InParty(Myself)
	Global("tb#StivanJaheiraKhalid","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("tb#StivanJaheiraKhalid","GLOBAL",1)
		RealSetGlobalTimer("tb#StivanJaheiraTimer","GLOBAL",1200)
END

IF
	RealGlobalTimerExpired("tb#StivanJaheiraTimer","GLOBAL")
	Global("tb#StivanJaheiraKhalid","GLOBAL",1)
	InParty("Jaheira")
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck("Jaheira",CD_STATE_NOTVALID)
	InParty(Myself)
	CombatCounter(0)
	!RealGlobalTimerNotExpired("tb#StivanNoSpam","GLOBAL")
THEN
	RESPONSE #80
		RealSetGlobalTimer("tb#StivanNoSpam","GLOBAL",20)
	RESPONSE #20
		RealSetGlobalTimer("tb#StivanNoSpam","GLOBAL",200)
		SetGlobal("tb#StivanJaheiraKhalid","GLOBAL",2)
		StartDialogueNoSet("jaheira")
END

IF
	InParty("Jaheira")
	See("Jaheira")
	!StateCheck("Jaheira",CD_STATE_NOTVALID)
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	CombatCounter(0)
	Global("tb#StivanJaheiraTrademeet","GLOBAL",0)
	AreaCheck("AR2000")
	!RealGlobalTimerNotExpired("tb#StivanNoSpam","GLOBAL")
THEN
	RESPONSE #80
		RealSetGlobalTimer("tb#StivanNoSpam","GLOBAL",20)
	RESPONSE #20
		RealSetGlobalTimer("tb#StivanNoSpam","GLOBAL",200)
		StartDialogueNoSet("Jaheira")
END

IF
InParty("Jaheira")
See("Jaheira")
!StateCheck("Jaheira",CD_STATE_NOTVALID)
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
Global("JaheiraCursed","GLOBAL",3)
CombatCounter(0)
Global("tb#StivanJaheiraCursed","GLOBAL",0)
THEN
	RESPONSE #100
		StartDialogueNoSet("Jaheira")
END

IF
InParty("Jaheira")
See("Jaheira")
!StateCheck("Jaheira",CD_STATE_NOTVALID)
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
Global("JaheiraCursed","GLOBAL",10)
CombatCounter(0)
Global("tb#StivanJaheiraCursed","GLOBAL",1)
	!RealGlobalTimerNotExpired("tb#StivanNoSpam","GLOBAL")
THEN
	RESPONSE #80
		RealSetGlobalTimer("tb#StivanNoSpam","GLOBAL",20)
	RESPONSE #20
		RealSetGlobalTimer("tb#StivanNoSpam","GLOBAL",200)
		StartDialogueNoSet("Jaheira")
END

IF
	InParty("Valygar")
	See("Valygar")
	!StateCheck("Valygar",CD_STATE_NOTVALID)
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	CombatCounter(0)
	Global("tb#StivanValygarSphere","GLOBAL",0)
	Dead("obshal04")
	!RealGlobalTimerNotExpired("tb#StivanNoSpam","GLOBAL")
THEN
	RESPONSE #80
		RealSetGlobalTimer("tb#StivanNoSpam","GLOBAL",20)
	RESPONSE #20
		RealSetGlobalTimer("tb#StivanNoSpam","GLOBAL",200)
		StartDialogueNoSet("Valygar")
END

IF
	InParty("Mazzy")
	See("Mazzy")
	!StateCheck("Mazzy",CD_STATE_NOTVALID)
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	CombatCounter(0)
	Global("tb#StivanMazzyObshal01","GLOBAL",0)
	Dead("obshal01")
	Dead("obshal02")
	Dead("obshal03")
THEN
	RESPONSE #100
		StartDialogueNoSet("Mazzy")
END

IF
	InParty("Keldorn")
	See("Keldorn")
	!StateCheck("Keldorn",CD_STATE_NOTVALID)
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	AreaCheck("AR0202")
	CombatCounter(0)
	Global("tb#StivanKeldornStink","GLOBAL",0)
	!RealGlobalTimerNotExpired("tb#StivanNoSpam","GLOBAL")
THEN
	RESPONSE #80
		RealSetGlobalTimer("tb#StivanNoSpam","GLOBAL",20)
	RESPONSE #20
		RealSetGlobalTimer("tb#StivanNoSpam","GLOBAL",200)
		StartDialogueNoSet("Keldorn")
END
	
IF
	InParty("Mazzy")
	See("Mazzy")
	!StateCheck("Mazzy",CD_STATE_NOTVALID)
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	CombatCounter(0)
	Global("tb#StivanMazzyDragon","GLOBAL",0)
	AreaCheck("ar1402")
THEN
	RESPONSE #100
		StartDialogueNoSet("Mazzy")
END

IF
	InParty("Mazzy")
	See("Mazzy")
	!StateCheck("Mazzy",CD_STATE_NOTVALID)
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	CombatCounter(0)
	Global("tb#StivanMazzyDrow","GLOBAL",0)
	GlobalGT("PhaereJob","GLOBAL",5)
	!RealGlobalTimerNotExpired("tb#StivanNoSpam","GLOBAL")
THEN
	RESPONSE #80
		RealSetGlobalTimer("tb#StivanNoSpam","GLOBAL",20)
	RESPONSE #20
		RealSetGlobalTimer("tb#StivanNoSpam","GLOBAL",200)
		StartDialogueNoSet("mazzy")
END

IF
	InParty("Mazzy")
	See("Mazzy")
	!StateCheck("Mazzy",CD_STATE_NOTVALID)
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	CombatCounter(0)
	Global("tb#StivanMazzyDrow","GLOBAL",1)
	AreaCheck("ar2500")
THEN
	RESPONSE #100
		StartDialogueNoSet("mazzy")
END

IF
	InParty("Haerdalis")
	See("Haerdalis")
	!StateCheck("Haerdalis",CD_STATE_NOTVALID)
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	HPPercentLT("tb#Stiv",30)
	CombatCounter(0)
	Global("tb#StivanHaerdalis","GLOBAL",3)
	!RealGlobalTimerNotExpired("tb#StivanNoSpam","GLOBAL")
THEN
	RESPONSE #80
		RealSetGlobalTimer("tb#StivanNoSpam","GLOBAL",20)
	RESPONSE #20
		RealSetGlobalTimer("tb#StivanNoSpam","GLOBAL",200)
		StartDialogueNoSet("haerdalis")
END

IF
	InParty("Minsc")
	See("Minsc")
	!StateCheck("Minsc",CD_STATE_NOTVALID)
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	HPPercentLT("tb#Stiv",40)
	Global("tb#StivanMinsc","GLOBAL",2)
	!RealGlobalTimerNotExpired("tb#StivanNoSpam","GLOBAL")
THEN
	RESPONSE #80
		RealSetGlobalTimer("tb#StivanNoSpam","GLOBAL",20)
	RESPONSE #20
		RealSetGlobalTimer("tb#StivanNoSpam","GLOBAL",200)
		StartDialogueNoSet("Minsc")
END

IF
	InParty("tb#Stiv")
	See("tb#Stiv")
	!StateCheck("tb#Stiv",CD_STATE_NOTVALID)
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	CombatCounter(0)
	Global("CerndBeggar","GLOBAL",2)
	Global("tb#StivanCernd","GLOBAL",1)
THEN
	RESPONSE #100
		RealSetGlobalTimer("tb#StivanNoSpam","GLOBAL",200)
		StartDialogueNoSet("cernd")
END

IF
	InParty("cernd")
	See("cernd")
	!StateCheck("cernd",CD_STATE_NOTVALID)
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	CombatCounter(0)
	Global("tb#StivanCernd","GLOBAL",4)
	GlobalGT("SolaufeinJob","GLOBAL",7)
	PartyHasItem("misc9w")
	Dead("udsola01")
	!RealGlobalTimerNotExpired("tb#StivanNoSpam","GLOBAL")
THEN
	RESPONSE #80
		RealSetGlobalTimer("tb#StivanNoSpam","GLOBAL",20)
	RESPONSE #20
		RealSetGlobalTimer("tb#StivanNoSpam","GLOBAL",200)
		StartDialogueNoSet("cernd")
END
	
IF
	InParty("cernd")
	See("cernd")
	!StateCheck("cernd",CD_STATE_NOTVALID)
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	CombatCounter(0)
	Global("tb#StivanCernd","GLOBAL",4)
	Global("SolaufeinJob","GLOBAL",9)
	!Dead("udsola01")
	!RealGlobalTimerNotExpired("tb#StivanNoSpam","GLOBAL")
THEN
	RESPONSE #80
		RealSetGlobalTimer("tb#StivanNoSpam","GLOBAL",20)
	RESPONSE #20
		RealSetGlobalTimer("tb#StivanNoSpam","GLOBAL",200)
		StartDialogueNoSet("cernd")
END

IF
	InParty("cernd")
	See("cernd")
	!StateCheck("cernd",CD_STATE_NOTVALID)
	!StateCheck("tb#stiv",CD_STATE_NOTVALID)
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	Global("tb#StivanCernd","GLOBAL",4)
	AreaCheck("ar2401")
THEN
	RESPONSE #100
		StartDialogueNoSet("cernd")
END

IF
	InParty("cernd")
	See("cernd")
	!StateCheck("cernd",CD_STATE_NOTVALID)
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	Global("tb#StivanCernd","GLOBAL",6)
	Global("tb#stivanentrytest","global",7)
THEN
	RESPONSE #100
		RealSetGlobalTimer("tb#StivanCerndTimer","GLOBAL",120)
		SetGlobal("tb#StivanCernd","GLOBAL",7)
END

IF
	InParty("cernd")
	See("cernd")
	!StateCheck("cernd",CD_STATE_NOTVALID)
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	Global("tb#StivanCernd","GLOBAL",7)
	AreaType(OUTDOOR)
	RealGlobalTimerExpired("tb#StivanCerndTimer","global")
	!RealGlobalTimerNotExpired("tb#StivanNoSpam","GLOBAL")
THEN
	RESPONSE #80
		RealSetGlobalTimer("tb#StivanNoSpam","GLOBAL",20)
	RESPONSE #20
		RealSetGlobalTimer("tb#StivanNoSpam","GLOBAL",200)
		StartDialogueNoSet("cernd")
END

IF
	InParty("cernd")
	See("cernd")
	!StateCheck("cernd",CD_STATE_NOTVALID)
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	Global("tb#StivanCernd","GLOBAL",8)
	PartyHasItem("miscb2")
THEN
	RESPONSE #100
		StartDialogueNoSet("cernd")
END

IF
	InParty("Anomen")
	See("Anomen")
	!StateCheck("Anomen",CD_STATE_NOTVALID)
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	CombatCounter(0)
	Global("AnomenDecide","GLOBAL",4)
	Global("tb#AnomenStivanSaerk","GLOBAL",0)
	AreaCheck("AR1002")
THEN
	RESPONSE #100
		StartDialogueNoSet("Anomen")
END

IF
InParty("Anomen")
See("Anomen")
!StateCheck("Anomen",CD_STATE_NOTVALID)
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
Global("tb#AnomenStivanSaerk","GLOBAL",1)
AreaCheck("AR1000")
THEN
	RESPONSE #100
		StartDialogueNoSet("Anomen")
END

IF
	InParty("Cernd")
	See("Cernd")
	!StateCheck("Cernd",CD_STATE_NOTVALID)
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	CombatCounter(0)
	Global("tb#StivanCernd","GLOBAL",3)
	GlobalGT("AsylumPlot","GLOBAL",1)
	!RealGlobalTimerNotExpired("tb#StivanNoSpam","GLOBAL")
THEN
	RESPONSE #80
		RealSetGlobalTimer("tb#StivanNoSpam","GLOBAL",20)
	RESPONSE #20
		RealSetGlobalTimer("tb#StivanNoSpam","GLOBAL",200)
		StartDialogueNoSet("Cernd")
END

IF
	InParty("tb#stiv")
	InParty(Player1)
	!StateCheck("tb#stiv",CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	OR(2)
		AreaCheck("AR0509")
		AreaCheck("AR0522")
	Global("tb#StivanPCTalk","GLOBAL",1)
THEN
	RESPONSE #100
		StartDialogueNoSet(Player1)
END

IF
	InParty("Valygar")
	See("Valygar")
	!StateCheck("Valygar",CD_STATE_NOTVALID)
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	Global("u!SunaDies","GLOBAL",1)
	CombatCounter(0)
	Global("tb#StivanValygarAboutSuna","GLOBAL",0)
THEN
	RESPONSE #100
		StartDialogueNoSet("valygar")
END
